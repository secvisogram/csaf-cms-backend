plugins {
	id 'org.springframework.boot' version '2.7.4'
	id 'io.spring.dependency-management' version '1.0.14.RELEASE'
	id 'java'
	id 'com.github.spotbugs' version '5.0.12'
	id 'jacoco'
	id 'com.github.jk1.dependency-license-report' version '2.0'
}

group = 'de.bsi.secvisogram'
version = '1.0.0'

repositories {
	mavenCentral()
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
		vendor = JvmVendorSpec.ADOPTIUM
	}
}

springBoot {
	buildInfo()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-mustache'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.security:spring-security-oauth2-resource-server'
	implementation 'org.springframework.security:spring-security-oauth2-jose'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	implementation 'com.ibm.cloud:cloudant:0.4.1'
	implementation 'com.github.spotbugs:spotbugs-annotations:4.7.3'

	implementation 'io.swagger.core.v3:swagger-annotations:2.2.6'
	implementation 'org.springdoc:springdoc-openapi-ui:1.6.14'
	implementation 'org.apache.commons:commons-text:1.10.0'
	implementation 'com.vdurmont:semver4j:3.1.0'


	implementation 'org.graalvm.js:js:22.2.0'
	implementation 'org.graalvm.js:js-scriptengine:22.2.0'

	//https://github.com/flipkart-incubator/zjsonpatch
	implementation ('com.flipkart.zjsonpatch:zjsonpatch:0.4.13') {
		exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
	}

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation ('org.openapitools:openapi-generator:6.2.0') {
		exclude group: 'io.swagger.core.v3', module: 'swagger-models' // conflict with swagger-annotations
	}
	testImplementation ('org.openapitools:openapi-generator-cli:6.2.0') {
		exclude group: 'io.swagger.core.v3', module: 'swagger-models' // conflict with swagger-annotations
	}

	testImplementation 'org.testcontainers:testcontainers:1.17.6'
	testImplementation 'org.testcontainers:junit-jupiter:1.17.6'
	testImplementation 'org.mockito:mockito-inline:4.11.0'

	spotbugsSlf4j 'org.slf4j:slf4j-simple:2.0.5'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'

	spotbugsPlugins 'com.mebigfatguy.sb-contrib:sb-contrib:7.4.7'
}

test {
	useJUnitPlatform()
	filter {
		excludeTestsMatching "de.bsi.secvisogram.csaf_cms_backend.OpenApiGenerateDocuTest"
	}
	testLogging.showStandardStreams = false
}

task apiDocumentation(type: Test, description: "generates the API documentation") {
	useJUnitPlatform()
	filter {
		includeTestsMatching "de.bsi.secvisogram.csaf_cms_backend.OpenApiGenerateDocuTest"
	}
}

tasks.withType(JavaCompile) {
	options.compilerArgs += ['-Xlint:deprecation']
}

jacoco {
	toolVersion = "0.8.7"
//	reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

jacocoTestReport {
	reports {
		xml.required = true
		csv.required = true
		html.outputLocation = layout.buildDirectory.dir('reports/jacocoHtml')
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: 'de/bsi/secvisogram/csaf_cms_backend/SecvisogramApplication.class')
		}))
	}

}

spotbugs {
	ignoreFailures = false
	showStackTraces = true
	showProgress = true
	effort = 'default'
	reportLevel = 'default'
//	visitors = [ 'FindSqlInjection', 'SwitchFallthrough' ]
//	omitVisitors = [ 'FindNonShortCircuit' ]
	reportsDir = file("$buildDir/spotbugs")
//	onlyAnalyze = [ 'com.foobar.MyClass', 'com.foobar.mypkg.*' ]
	maxHeapSize = '1g'
	extraArgs = [ '-nested:false' ]
	jvmArgs = [ '-Duser.language=de' ]
}

spotbugsMain {
	reports {
		html {
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/main/spotbugsMain.html")
			stylesheet = 'fancy-hist.xsl'
		}
		xml {
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/xml/spotbugsMain.xml")
		}

	}
}

spotbugsTest {
	reports {
		html {
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/main/spotbugsTest.html")
			stylesheet = 'fancy-hist.xsl'
		}
		xml {
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/xml/spotbugsTest.xml")
		}

	}
}

import com.github.jk1.license.render.*
licenseReport {
    // Set output directory for the report data.
    // Defaults to ${project.buildDir}/reports/dependency-license.
    outputDir = "$buildDir/licenses"

    // Select projects to examine for dependencies.
    // Defaults to current project and all its subprojects
    projects = [project] + project.subprojects

    // Adjust the configurations to fetch dependencies. Default is 'runtimeClasspath'
    // For Android projects use 'releaseRuntimeClasspath' or 'yourFlavorNameReleaseRuntimeClasspath'
    // Use 'ALL' to dynamically resolve all configurations:
    // configurations = ALL
    configurations = ['runtimeClasspath']

    // List the groups ids to exclude from dependency report. Supports regular expressions.
    // For finer granularity, see: excludes.
    //excludeGroups = ['do.not.want']

    // List the ids (in module:name format) to exclude from dependency report. Supports regular expressions.
    // By default excludes is empty.
    //excludes = ['moduleGroup:moduleName']

    // Don't include artifacts of project's own group into the report
    excludeOwnGroup = true

    // Don't exclude bom dependencies.
    // If set to true, then all boms will be excluded from the report
    excludeBoms = false

    // Set custom report renderer, implementing ReportRenderer.
    // Yes, you can write your own to support any format necessary.
    renderers = [
     new XmlReportRenderer('third-party-libs.xml', 'CSAF CMS Libraries'),
     new TextReportRenderer('third-party-libs.txt'), 
     new InventoryHtmlReportRenderer('index.html', 'Some Title')]

    // Set importers to import any external dependency information, i.e. from npm.
    // Custom importer should implement DependencyDataImporter interface.
    //importers = [new XmlReportImporter('Frontend dependencies', file(frontend_libs.xml))]

    // This is for the allowed-licenses-file in checkLicense Task
    // Accepts File, URL or String path to local or remote file
    // allowedLicensesFile = new File("$projectDir/config/allowed-licenses.json")
}
