plugins {
	id 'org.springframework.boot' version '3.1.1'
	id 'io.spring.dependency-management' version '1.1.1'
	id 'java'
	id 'com.github.spotbugs' version '5.0.14'
	id 'jacoco'
	id 'org.springdoc.openapi-gradle-plugin' version '1.6.0'
	id 'org.openapi.generator' version '6.6.0'
}

group = 'de.bsi.secvisogram'
version = '1.0.0'

repositories {
	mavenCentral()
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
		vendor = JvmVendorSpec.ADOPTIUM
	}
}

springBoot {
	buildInfo()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-mustache'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.security:spring-security-oauth2-resource-server'
	implementation 'org.springframework.security:spring-security-oauth2-jose'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	implementation 'com.ibm.cloud:cloudant:0.5.3'
	implementation 'com.github.spotbugs:spotbugs-annotations:4.7.3'

	implementation 'io.swagger.core.v3:swagger-annotations:2.2.15'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'
	implementation 'org.apache.commons:commons-text:1.10.0'
	implementation 'com.vdurmont:semver4j:3.1.0'


	implementation 'org.graalvm.js:js:23.0.0'
	implementation 'org.graalvm.js:js-scriptengine:23.0.0'

	//https://github.com/flipkart-incubator/zjsonpatch
	implementation ('com.flipkart.zjsonpatch:zjsonpatch:0.4.14') {
		exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
	}

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation ('org.openapitools:openapi-generator:6.6.0') {
		exclude group: 'io.swagger.core.v3', module: 'swagger-models' // conflict with swagger-annotations
	}
	testImplementation ('org.openapitools:openapi-generator-cli:6.6.0') {
		exclude group: 'io.swagger.core.v3', module: 'swagger-models' // conflict with swagger-annotations
	}

	testImplementation 'org.testcontainers:testcontainers:1.18.3'
	testImplementation 'org.testcontainers:junit-jupiter:1.18.3'
	testImplementation 'org.mockito:mockito-inline:5.2.0'

	spotbugsSlf4j 'org.slf4j:slf4j-simple:2.0.7'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'

	spotbugsPlugins 'com.mebigfatguy.sb-contrib:sb-contrib:7.4.7'
}

test {
	useJUnitPlatform()
	testLogging.showStandardStreams = false
}

tasks.withType(JavaCompile) {
	options.compilerArgs += ['-Xlint:deprecation,unchecked']
}

jacoco {
	toolVersion = "0.8.7"
}

jacocoTestReport {
	reports {
		xml.required = true
		csv.required = true
		html.outputLocation = layout.buildDirectory.dir('reports/jacocoHtml')
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: 'de/bsi/secvisogram/csaf_cms_backend/SecvisogramApplication.class')
		}))
	}

}

spotbugs {
	ignoreFailures = false
	showStackTraces = true
	showProgress = true
	effort = 'default'
	reportLevel = 'default'
//	visitors = [ 'FindSqlInjection', 'SwitchFallthrough' ]
//	omitVisitors = [ 'FindNonShortCircuit' ]
	reportsDir = file("$buildDir/spotbugs")
//	onlyAnalyze = [ 'com.foobar.MyClass', 'com.foobar.mypkg.*' ]
	maxHeapSize = '1g'
	extraArgs = [ '-nested:false' ]
	jvmArgs = [ '-Duser.language=de' ]
}

spotbugsMain {
	reports {
		html {
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/main/spotbugsMain.html")
			stylesheet = 'fancy-hist.xsl'
		}
		xml {
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/xml/spotbugsMain.xml")
		}

	}
}

spotbugsTest {
	reports {
		html {
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/main/spotbugsTest.html")
			stylesheet = 'fancy-hist.xsl'
		}
		xml {
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/xml/spotbugsTest.xml")
		}

	}
}

task apiDocumentation(type: GradleBuild) {
    tasks = [
      	'buildHtmlDoc','copyDoc']
}

openApi {
    apiDocsUrl.set("http://localhost:8081/api-docs")
    outputDir.set(file("$buildDir/docs"))
    outputFileName.set("openapi3.json")
    waitTimeInSeconds.set(10)
}

task buildHtmlDoc(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask){
	dependsOn('generateOpenApiDocs')
    generatorName = "html"
    inputSpec = "$buildDir/docs/openapi3.json".toString()
    outputDir = "$buildDir/gen-docs/html".toString()
}

tasks.register('copyDoc', Copy) {
	dependsOn('buildHtmlDoc')
    from layout.buildDirectory.file("$buildDir/docs/openapi3.json"),layout.buildDirectory.file("$buildDir/gen-docs/html/index.html")
    into layout.buildDirectory.dir("$rootDir/docs")
}


